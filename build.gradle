plugins {
    id 'groovy'
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.malikzh'
version System.getenv('GITHUB_REF_NAME') ? System.getenv('GITHUB_REF_NAME') : '1.0.4'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

configurations {
    test
}

dependencies {
    // Groovy
    implementation 'org.codehaus.groovy:groovy:2.5.22'

    // Junit
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // Spock
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'org.spockframework:spock-junit4:2.3-groovy-2.5'

    // Lombok (for tests)
    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
    }

    dependsOn test // tests are required to run before generating the report
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'My Library'
                description = 'A concise description of my library'
                url = 'http://www.example.com/library'

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/malikzh/dtox/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'malikzh'
                        name = 'Malik Zharykov'
                        email = 'cmalikz.h@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/malikzh/dtox.git'
                    developerConnection = 'scm:git:ssh://github.com/malikzh/dtox.git'
                    url = 'https://github.com/malikzh/dtox'
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv('OSSRH_USERNAME')
                password = System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    def signingKey = System.getenv('GPG_KEY')
    def signingPassword = System.getenv('GPG_PASSWORD')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications['mavenJava']
}
